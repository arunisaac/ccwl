;;; ccwl --- Concise Common Workflow Language
;;; Copyright Â© 2021 Arun Isaac <arunisaac@systemreboot.net>
;;;
;;; This file is part of ccwl.
;;;
;;; ccwl is free software: you can redistribute it and/or modify it
;;; under the terms of the GNU General Public License as published by
;;; the Free Software Foundation, either version 3 of the License, or
;;; (at your option) any later version.
;;;
;;; ccwl is distributed in the hope that it will be useful, but
;;; WITHOUT ANY WARRANTY; without even the implied warranty of
;;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
;;; General Public License for more details.
;;;
;;; You should have received a copy of the GNU General Public License
;;; along with ccwl.  If not, see <https://www.gnu.org/licenses/>.

;;; Commentary:

;; This is a better test driver for Guile's (srfi srfi-64).
;;
;; TODO: Improve Guile's test driver so this module won't be
;; necessary.

;;; Code:

(use-modules (ice-9 format)
             (ice-9 getopt-long)
             (srfi srfi-26)
             (srfi srfi-64))

;; Currently, only log-file and trs-file are understood. Everything
;; else is ignored.
(define %options
  '((test-name (value #t))
    (log-file (value #t))
    (trs-file (value #t))
    (color-tests (value #t))
    (expect-failure (value #t))
    (enable-hard-errors (value #t))))

(define (color code str)
  (format #f "~a[~am~a~a[0m" #\esc code str #\esc))

(define red (cut color 31 <>))
(define green (cut color 32 <>))
(define magenta (cut color 35 <>))

(define (my-gnu-runner log-port trs-port)
  (let ((runner (test-runner-null)))
    (test-runner-on-group-begin! runner
      (lambda (runner suite-name count)
        (format #t (magenta "%%%% ~a~%") suite-name)))
    (test-runner-on-group-end! runner
      (lambda _
        (newline)))
    (test-runner-on-test-end! runner
      (lambda (runner)
        (let ((name (test-runner-test-name runner))
              (result (string-upcase
                       (symbol->string (test-result-kind runner))))
              (result-alist (test-result-alist runner)))
          (format trs-port ":test-result: ~a ~a~%" result name)
          (format #t "~a ~a~%"
                  (case (test-result-kind runner)
                    ((pass) (green result))
                    (else (red result)))
                  name)
          (format log-port "~a ~a~%" result name)
          ;; If test did not pass, print details.
          (unless (eq? (test-result-kind runner) 'pass)
            (let ((log-output
                   (format #f "~a:~a~%expected: ~s~%actual: ~s~%"
                           (assq-ref result-alist 'source-file)
                           (assq-ref result-alist 'source-line)
                           (assq-ref result-alist 'expected-value)
                           (assq-ref result-alist 'actual-value))))
              (display log-output log-port)
              (display log-output (current-error-port)))))))
    runner))

(let ((opts (getopt-long (command-line) %options)))
  (call-with-output-file (string-append "@abs_top_builddir@/"
                                        (option-ref opts 'log-file #f))
    (lambda (log-port)
      (call-with-output-file (option-ref opts 'trs-file #f)
        (lambda (trs-port)
          (chdir "@abs_top_srcdir@")
          (test-with-runner (my-gnu-runner log-port trs-port)
            (load-from-path (option-ref opts 'test-name #f))))))))
